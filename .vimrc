" ä¼šç¤¾ã§ã®ä½œæ¥­ç”¨è¨­å®š
if !exists("g:office_work") | let g:office_work = v:false | endif
if !exists("g:help_translation") | let g:help_translation = v:false | endif

unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

if g:help_translation
  " settings for vimdoc-ja-working
  set fileencoding=utf-8
  set fileformat=unix
  set encoding=utf-8
  let g:autofmt_allow_over_tw = 1
  set formatoptions+=mM
endif

scriptencoding utf-8

" ãƒãƒƒã‚¯ã‚¢ãƒƒãƒ—ç”¨ãƒ•ã‚¡ã‚¤ãƒ«ã¨undoç”¨ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ã€å…ƒãƒ•ã‚¡ã‚¤ãƒ«ã®å ´æ‰€ã§ã¯ãªãä¸€ç®‡æ‰€ã«ã¾ã¨ã‚ã‚‹ã€‚
" swapãƒ•ã‚¡ã‚¤ãƒ«ã‚‚è¿½åŠ 
let s:cache_home = empty($XDG_CACHE_HOME) ? expand('~/.cache') : $XDG_CACHE_HOME
let s:bak_path = s:cache_home . "/vim/bak"
let s:undo_path = s:cache_home . "/vim/undo"
let s:swap_path = s:cache_home . "/vim/swap"
if !isdirectory(s:bak_path) | call mkdir(s:bak_path, "p") | endif
if !isdirectory(s:undo_path) | call mkdir(s:undo_path, "p") | endif
if !isdirectory(s:swap_path) | call mkdir(s:swap_path, "p") | endif
let &backupdir = s:bak_path
let &undodir = s:undo_path
let &directory = s:swap_path
set backup
set writebackup
set undofile
set swapfile

if !has('kaoriya')

  set t_Co=256
  colorscheme torte
  set fileencodings=euc-jp,cp932,utf-8,euc-jisx0213,ucs-bom,ucs-2le,ucs-2,iso-2022-jp-3

  " https://qiita.com/mwmsnn/items/0b40662a22162907efae
  " æŒ¿å…¥ãƒ¢ãƒ¼ãƒ‰ã«å…¥ã‚‹æ™‚ï¼Œå‰å›ã®æŒ¿å…¥ãƒ¢ãƒ¼ãƒ‰ã«ãŠã‘ã‚‹ IME ã®çŠ¶æ…‹ã‚’å¾©å…ƒã™ã‚‹ï¼
  " Tera Termã§ã—ã‹å‹•ã„ã¦ã„ãªã„ã€‚ã€‚ã€‚
  " set t_SI+=[<r
  " æŒ¿å…¥ãƒ¢ãƒ¼ãƒ‰ã‚’å‡ºã‚‹æ™‚ï¼Œç¾åœ¨ã® IME ã®çŠ¶æ…‹ã‚’ä¿å­˜ã—ï¼ŒIME ã‚’ã‚ªãƒ•ã«ã™ã‚‹ï¼
  " set t_EI+=[<s[<0t
  " Vim çµ‚äº†æ™‚ï¼ŒIME ã‚’ç„¡åŠ¹ã«ã—ï¼Œç„¡åŠ¹ã«ã—ãŸçŠ¶æ…‹ã‚’ä¿å­˜ã™ã‚‹ï¼
  " set t_te+=[<0t[<s
  " ESC ã‚­ãƒ¼ã‚’æŠ¼ã—ã¦ã‹ã‚‰æŒ¿å…¥ãƒ¢ãƒ¼ãƒ‰ã‚’å‡ºã‚‹ã¾ã§ã®æ™‚é–“ã‚’çŸ­ãã™ã‚‹
  " set ttimeoutlen=100
  inoremap <silent> <esc> <esc>:call system('ibus engine "xkb:us::eng"')<cr>

  filetype on
  filetype plugin on
  filetype indent on

  " (:bro olã§è¡¨ç¤ºã•ã‚Œã‚‹)ãƒ•ã‚¡ã‚¤ãƒ«ã®å±¥æ­´ã‚’60ã¾ã§ã«åˆ¶é™ã™ã‚‹ã€‚ãã®ä»–ã¯ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®è¨­å®šã€‚
  set viminfo='60,<50,s10,h

else

  if g:office_work
    " guessã‚’ä½¿ã„ãŸã„ã€ãŒã€ä¼šç¤¾ã®ãƒ•ã‚¡ã‚¤ãƒ«ã¯ã»ã¨ã‚“ã©euc-jpãªã®ã§ã€ãã‚Œã‚ˆã‚Šå‰ã«æŒã£ã¦ãã‚‹
    set fileencodings=euc-jp,guess,ucs-bom,ucs-2le,ucs-2,iso-2022-jp-3,utf-8,euc-jisx0213
    " grepã®çµæœã‚’(euc-jpã‹ã‚‰)shift_jisã«(Gtagsã®çµæœã«ã¤ã„ã¦ã¯gtags.vimã§å¯¾ç­–)
    set shellpipe=2>\&1\ \|\ nkf32\ -s\ >\ %s
  endif

  " ã†ãƒ¼ã‚“ã€defaultã®è¨­å®šã¨ã©ã£ã¡ãŒã„ã„ã‹åˆ†ã‹ã‚‰ã‚“ã€‚å¾Œã§æ¶ˆã™ã‹ã‚‚ã€‚ã€‚ã€‚
  " insert modeã‚’å‡ºã‚‹ã¨ãã«IMEã‚’offã«ã™ã‚‹ -> å¿…ãšIME offã§é–‹å§‹ã™ã‚‹
  inoremap <silent> <esc> <esc>:set iminsert=0<cr>

  augroup transparency
    autocmd!
    autocmd FocusLost * set transparency=200
    autocmd FocusGained * set transparency=255
  augroup END

  function! g:RestoreMouse(timer)
    " 8.1.0011ã§ã‚‚ã£ã¨ç°¡å˜ã«èª¿ã¹ã‚‹æ–¹æ³•ãŒå…¥ã£ãŸã‘ã©ã¾ã ä½¿ãˆãªã„ã€‚
    " if !empty(mapcheck('<leftmouse>', 'n'))
    let l:mapping = maparg('<leftmouse>', 'n', v:false, v:true)
    if has_key(l:mapping, 'rhs') && l:mapping['rhs'] ==? '<nop>'
      unmap <leftmouse>
    endif
    let l:mapping = maparg('<leftmouse>', 'i', v:false, v:true)
    if has_key(l:mapping, 'rhs') && l:mapping['rhs'] ==? '<nop>'
      unmap! <leftmouse>
    endif
  endfunction

  augroup disable_mouse_click
    " éã‚¢ã‚¯ãƒ†ã‚£ãƒ–ã®çŠ¶æ…‹ã§ã®ä¸€ç™ºç›®ã®ãƒã‚¦ã‚¹ã‚¯ãƒªãƒƒã‚¯ã§ã¯ã‚«ãƒ¼ã‚½ãƒ«ã‚’ç§»å‹•ã•ã›ãªã„
    " FocusGainedã ã‘ã§å‡¦ç†ã—ã‚ˆã†ã¨ã—ã¦ã‚‚ã€ãƒã‚¦ã‚¹ã‚¤ãƒ™ãƒ³ãƒˆã¯æ—¢ã«ç™ºç”Ÿã—ã¦ã„ã¦æ‰‹é…ã‚Œ
    autocmd!
    autocmd FocusLost * noremap <leftmouse> <nop>
    autocmd FocusLost * noremap! <leftmouse> <nop>
    autocmd FocusGained * call timer_start(100, 'g:RestoreMouse')
  augroup END

endif

"dein Scripts-----------------------------
if &compatible
  set nocompatible               " Be iMproved
endif

" reset augroup
augroup MyAutoCmd
  autocmd!
augroup END

" https://qiita.com/kawaz/items/ee725f6214f91337b42b
" deinè‡ªä½“ã®è‡ªå‹•ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«
let s:dein_dir = s:cache_home . '/dein'
let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'
if !isdirectory(s:dein_repo_dir)
  call system('git clone https://github.com/Shougo/dein.vim ' . shellescape(s:dein_repo_dir))
endif
let &runtimepath = s:dein_repo_dir .",". &runtimepath
" ãƒ—ãƒ©ã‚°ã‚¤ãƒ³èª­ã¿è¾¼ã¿ï¼†ã‚­ãƒ£ãƒƒã‚·ãƒ¥ä½œæˆ
let s:toml_file = fnamemodify(expand('<sfile>'), ':h').'/.dein.toml'
if dein#load_state(s:dein_dir)
  call dein#begin(s:dein_dir)
  call dein#load_toml(s:toml_file)
  call dein#end()
endif
let g:dein#types#git#clone_depth = 1
" ä¸è¶³ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã®è‡ªå‹•ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«
if has('vim_starting') && dein#check_install()
  call dein#install()
endif

" Required:
filetype plugin indent on
syntax enable
"End dein Scripts-------------------------

set title titlestring=%F
set number

set ignorecase
set smartcase
set nowrapscan
set incsearch
set hlsearch

set grepprg=git\ grep\ --line-number

" æ—¢ã«ãƒ•ã‚¡ã‚¤ãƒ«å†…ã«ã‚ã‚‹ã‚¿ãƒ–æ–‡å­—ã‚’ç©ºç™½ä½•å€‹åˆ†ã§è¡¨ç¤ºã™ã‚‹ã‹
set tabstop=4
" tabstopã«åˆã‚ã›ã‚‹
set shiftwidth=0
" shiftwidthã«åˆã‚ã›ã‚‹ -> ã‚„ã£ã±ã‚Šä½¿ã‚ãªã„
set softtabstop=0
" ã‚¿ãƒ–æ–‡å­—ã®ä»£ã‚ã‚Šã«ç©ºç™½ã‚’å…¥åŠ›ã™ã‚‹
set expandtab

set autoindent
set smartindent

set nowrap
set showmatch
set matchtime=1
set list
set listchars=tab:>-,trail:-
set cursorline

set showcmd
set cmdheight=2
set laststatus=2

set wildmenu
set wildmode=longest:full,full

set nostartofline

set nrformats-=octal

set path+=**
" ã“ã® path ã®è¨­å®šã ã¨æ™‚é–“ãŒã‹ã‹ã‚Šã™ãã‚‹ã®ã§ include ã‚’é™¤å¤–
set complete-=i

set diffopt+=vertical

set clipboard+=unnamed

augroup WindowLocalOptions
  autocmd!
  autocmd BufWinEnter * set nofoldenable
augroup END

"ãƒã‚¤ãƒŠãƒªç·¨é›†(xxd)ãƒ¢ãƒ¼ãƒ‰ï¼ˆvim -b ã§ã®èµ·å‹•ã€ã‚‚ã—ãã¯ *.bin ãƒ•ã‚¡ã‚¤ãƒ«ã‚’é–‹ãã¨ç™ºå‹•ã—ã¾ã™ï¼‰
augroup BinaryXXD
  autocmd!
  autocmd BufReadPre  *.bin let &binary =1

  autocmd BufReadPost * if &binary
  autocmd BufReadPost *   silent %!xxd -g 1
  autocmd BufReadPost *   set ft=xxd
  autocmd BufReadPost * endif

  autocmd BufWritePre * if &binary
  autocmd BufWritePre *   %!xxd -r
  autocmd BufWritePre * endif

  autocmd BufWritePost * if &binary
  autocmd BufWritePost *   silent %!xxd -g 1
  autocmd BufWritePost *   set nomod
  autocmd BufWritePost * endif
augroup END

" shellå´ã§å¯¾å¿œã™ã‚‹ï¼Ÿ
" git status --short | grep "^ M " | cut -c 4- | xargs -I FILE nkf32 -ex --in-place FILE
if g:office_work
  augroup ConvertEucjp
    autocmd!
    autocmd BufWritePost * if &fileencoding ==# "euc-jp"
    autocmd BufWritePost *   let b:DoConvert = v:true
    autocmd BufWritePost * endif
    " BufUnload -> BufDelete -> BufWipeout
    " Vimã‚’çµ‚äº†ã—ãŸã¨ãã¯BufUnloadã—ã‹å‘¼ã°ã‚Œãªã„ã£ã½ã„ï¼Ÿã‚ˆãåˆ†ã‹ã‚‰ãªã„
    autocmd BufLeave * if exists("b:DoConvert") && b:DoConvert
"     autocmd BufLeave *   let b:orgAutoRead = &autoread
"     autocmd BufLeave *   setlocal autoread
    autocmd BufLeave *   silent !nkf32 -Eex --in-place %
    autocmd BufLeave *   e
"     autocmd BufLeave *   if b:orgAutoRead == v:false
"     autocmd BufLeave *     setlocal noautoread
"     autocmd BufLeave *   endif
    autocmd BufLeave *   let b:DoConvert = v:false
    autocmd BufLeave * endif
    autocmd BufUnload * if exists("b:DoConvert") && b:DoConvert
    autocmd BufUnload *   !start nkf32 -Eex --in-place %
    autocmd BufUnload *   let b:DoConvert = v:false
    autocmd BufUnload * endif
  augroup END
endif

nnoremap <space>h :tabp<cr>
nnoremap <space>l :tabn<cr>
nnoremap <c-w><space>h <c-w>:tabp<cr>
nnoremap <c-w><space>l <c-w>:tabn<cr>
tnoremap <c-w><space>h <c-w>:tabp<cr>
tnoremap <c-w><space>l <c-w>:tabn<cr>

" ãƒªãƒ¢ãƒ¼ãƒˆç’°å¢ƒã§ã¯<ctrl + ç‰¹æ®Šã‚­ãƒ¼>ã¯ã»ã¨ã‚“ã©å‹•ã‹ãªã„
if !has('kaoriya')
  " <c-i> ãŒç½®ãæ›ã‚ã£ã¦ã—ã¾ã†
  " noremap <tab> :tabn<cr>
  noremap <a-right> :tabn<cr>
  noremap <a-left> :tabp<cr>
  tnoremap <a-right> <c-w>:tabn<cr>
  tnoremap <a-left> <c-w>:tabp<cr>
else
  " ctrl-tabã§æ¬¡ã®tabã«é€²ã‚€
  noremap <c-tab> :tabn<cr>
  noremap <c-s-tab> :tabp<cr>
  inoremap <c-tab> <esc>:tabn<cr>
  inoremap <c-s-tab> <esc>:tabp<cr>
  tnoremap <c-tab> <c-w>:tabn<cr>
  tnoremap <c-s-tab> <c-w>:tabp<cr>
  " ctrl-+/ctrl--ã§tabã‚’éš£ã«ç§»å‹•
  noremap <c-kPlus> :tabm+<cr>
  noremap <c-kMinus> :tabm-<cr>
endif

" æ”¹è¡Œã€æ™®æ®µä½¿ã‚ãªã„ã®ã§<cr>ã ã‘ã«ãƒãƒƒãƒ—ã—ãŸã„ã‘ã©ã€grepçµæœã«ã‚‚å½±éŸ¿ã‚ã‚‹ã®ã§è¦æ¤œè¨
noremap <c-cr> o<esc>

noremap <c-f4> :tabc<cr>
noremap <space>c :tabc<cr>
noremap <c-n> :tabnew<cr>
tnoremap <c-n> <c-w>:tabnew<cr>

" 3è¡Œãšã¤é€²ã‚€ã€3è¡Œãšã¤æˆ»ã‚‹
noremap <c-j> 3<c-e>
noremap <c-k> 3<c-y>
vnoremap <c-j> 3j
vnoremap <c-k> 3k
" è¡Œæœ«ã«ç§»å‹•ã€ã„ã¾ã„ã¡'f'ã‚„'t'ã®æ—¨ã¿ã‚’æ„Ÿã˜ãªã„
noremap ; $
vnoremap ; $h
" æ–‡å­—åˆ—ã®å…ˆé ­ã«ç§»å‹•(ã™ã§ã«å…ˆé ­ã§ã‚ã‚Œã°1åˆ—ç›®ã«ç§»å‹•)
noremap <silent> 0 :call <SID>GoToFirstColumn()<cr>
vnoremap 0 ^

" å¸¸ã«very magicã§æ¤œç´¢ã™ã‚‹
noremap  /  /\v
nnoremap /  :set imsearch=0<cr>/\v
nnoremap // :set imsearch=2<cr>/\v
" comment, uncomment
noremap <space><space> :call <SID>ToggleComment()<cr>
" æ¤œç´¢ã®å±¥æ­´ã‚’ãŸã©ã‚‹ã¨ãã¯very magicã‚’ã¯ãšã™
noremap  /<up> /<up>
nnoremap /<up> :set imsearch=0<cr>/<up>
" *ã‚’very magicã§æ¤œç´¢ã™ã‚‹ã‚ˆã†ã«ç½®ãæ›ãˆã‚‹ã€(é ã„)æ¬¡ã®æ¤œç´¢å€™è£œã«é£›ã‚“ã§ã—ã¾ã†ã®ãŒå«Œãªã®ã§<bs>ã§ä¸€å€‹æˆ»ã£ã¦ã‹ã‚‰æ¤œç´¢ã™ã‚‹
nnoremap * yiw<bs>/\v<c-r>0<cr>
" å˜èªã§æ¤œç´¢
nnoremap <space>* yiw<bs>/\v<<c-r>0><cr>
" æ¤œç´¢å¯¾è±¡ã‚’è¿½åŠ ã—ã¦ã„ã
nnoremap <bar> yiw<bs>/<up><bar><c-r>0<cr>
nnoremap <space><bar> yiw<bs>/<up><bar><<c-r>0><cr>
" é¸æŠç¯„å›²ã‚’ãã®ã¾ã¾(æ­£è¦è¡¨ç¾ã‚’ä½¿ã‚ãšã«)æ¤œç´¢ã™ã‚‹
vnoremap * y:let @" = escape(@", '/')<cr><bs>/\V<c-r>0<cr>
vnoremap <space>* y<bs>/\V\<<c-r>0\><cr>

" ç½®æ›("ctrl-r"ã«ã—ãŸã‹ã£ãŸãŒã€"r"ç³»ã¯ã„ã‚ã„ã‚ã¨ä½¿ã‚ã‚Œã¦ã„ã‚‹ã®ã§ä»£ã‚ã‚Šã«Officeç³»ã§ä½¿ã‚ã‚Œã‚‹"ctrl-h"ã‚’ä½¿ã†ã€‚)
nnoremap <c-h> :%s///g<left><left>
vnoremap <c-h> :s///g<left><left>

" grep
nnoremap <silent> <c-g> :call <SID>DoGrep()<cr>

nnoremap <expr> } <SID>CurlyBracket("}")
nnoremap <expr> { <SID>CurlyBracket("{")

let @d = '?^@/-l"aye?^---wll"bY:rightbelow vert new b:a'
tnoremap @d N@d

function! s:CurlyBracket(text)
  if a:text == "}"
    return &diff ? "]c" : ":cn\<cr>"
  elseif a:text == "{"
    return &diff ? "[c" : ":cp\<cr>"
  else
    " do nothing
  endif
endfunction

if g:office_work
  " ã‚»ã‚¯ã‚·ãƒ§ãƒ³(ãƒ¡ã‚½ãƒƒãƒ‰)é–“ç§»å‹•ãŒã†ã¾ãå‹•ã‹ãªã„ã‚±ãƒ¼ã‚¹ãŒã‚ã‚‹ã®ã§ã€ç°¡æ˜“çš„ãªãƒ¡ã‚½ãƒƒãƒ‰é–“ç§»å‹•æ–¹æ³•ã‚’å®šç¾©
  " nnoremap [[ ?\v::\w+\([^\)]*\)[^\{]*\n{0,1}\{<cr>
  " nnoremap ]] /\v::\w+\([^\)]*\)[^\{]*\n{0,1}\{<cr>

  " nnoremap <space><space> A // nishi 
endif

" é–¢æ•°ã£ã½ã„ã‚‚ã®ã‚’æ¤œç´¢(ãƒã‚¤ãƒ©ã‚¤ãƒˆ)
nnoremap <space>/ /\v\w+\(<cr>

" gtagsé–¢é€£ã€ctagsã‚‚ä¸€å¿œæ®‹ã™
if has('kaoriya')
  nnoremap ctags :!start ctags -R *<cr>
  nnoremap gtags :!start gtags -v<cr>
  nnoremap <f11> :Gtags -f %<cr>
  nnoremap <f12> :GtagsCursor<cr>
  nnoremap <c-f12> :sp<cr><c-w>T:GtagsCursor<cr>
  nnoremap <s-f12> :sp<cr><c-w>T:tabm-<cr>:Gtags -r <c-r><c-w><cr>
  " nnoremap <s-f12> :sp<cr><c-w>T:tabm-<cr>:execute("Gtags -r " . cfi#format('%s', '')[0:-3])<cr>
  nnoremap <c-f11> :vs<cr><c-w>l:GtagsCursor<cr>
  nnoremap <s-f11> :sp<cr>:GtagsCursor<cr>
else
  nnoremap ctags :!ctags -R *<cr>
  nnoremap gtags :!gtags -v<cr>
  nnoremap <f12> :GtagsCursor<cr>
  nnoremap <s-f12> :sp<cr><c-w>T:tabm-<cr>:Gtags -r <c-r><c-w><cr>
endif

" ç¾åœ¨ã®ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦ã‚’åˆ¥ã‚¿ãƒ–ã«ç§»å‹•ã™ã‚‹
nnoremap <f10> <c-w>T
nnoremap <c-f10> :sp<cr><c-w>T

noremap <a-h> <c-w>h
noremap <a-j> <c-w>j
noremap <a-k> <c-w>k
noremap <a-l> <c-w>l
inoremap <a-h> <esc><c-w>h
inoremap <a-j> <esc><c-w>j
inoremap <a-k> <esc><c-w>k
inoremap <a-l> <esc><c-w>l

" è¡Œæœ«ã¾ã§ãƒ¤ãƒ³ã‚¯
nnoremap Y y$
" ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰ã§ã®Windowsã‚¯ãƒªãƒƒãƒ—ãƒœãƒ¼ãƒ‰ã¸ã®å˜èªã‚³ãƒ”ãƒ¼
nnoremap <c-insert> viw"*y
" æ¯åº¦ãƒ¬ã‚¸ã‚¹ã‚¿ã‚’æŒ‡å®šã™ã‚‹ã®ãŒé¢å€’ãªã®ã§ã€a, b, cã ã‘ãƒ¤ãƒ³ã‚¯ã¨ãƒšãƒ¼ã‚¹ãƒˆã‚’å‰²ã‚Šå½“ã¦ã¦ãŠã
nnoremap <space>ya "ayiw
nnoremap <space>yb "byiw
nnoremap <space>yc "cyiw
nnoremap <space>pa "ap
nnoremap <space>pb "bp
nnoremap <space>pc "cp
" gpã¯å¸¸ã«ãƒ¬ã‚¸ã‚¹ã‚¿0ã‚’è²¼ã‚Šä»˜ã‘ã‚‹
noremap gp "0p

nnoremap <space>v :tabnew ~/.vimrc<cr>

noremap <c-z> :echo "nop"<cr>

" ã†ã£ã‹ã‚Šæ”¹è¡Œã—ã¦ã—ã¾ã£ãŸã¨ãã«ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã‚’ã™ã¹ã¦æ¶ˆã™ -> <expr>ã‚’ä½¿ã£ãŸã»ã†ãŒãã‚Œã„ã‹ã‚‚
" <c-o>ã ã¨undoãŒãŠã‹ã—ããªã‚‹
inoremap <silent> <bs> <c-r>=<SID>BsForInsertMode()<cr>

" cnoremap ( ()<left>
" cnoremap { {}<left>
" cnoremap [ []<left>
" cnoremap " ""<left>
" cnoremap ' ''<left>

nnoremap + :call <SID>ChangeFontSize(1)<cr>:echo &guifont<cr>
nnoremap - :call <SID>ChangeFontSize(-1)<cr>:echo &guifont<cr>

nnoremap <space>k :call <SID>MoveUpwardDownward(v:true)<cr>
nnoremap <space>j :call <SID>MoveUpwardDownward(v:false)<cr>

noremap! <expr> <c-r>/ <SID>PasteSlash()

" " è‡ªä½œã‚³ãƒãƒ³ãƒ‰ã‚µãƒ³ãƒ—ãƒ«(å¼•æ•°ãªã—ãªã‚‰nargsã¯è¦ã‚‰ãªã„ã‹ã‚‚)
" command! -nargs=0 MyFunc call s:MyFunc()
" 
" " ":help expression"ã¨ã‚„ã‚‹ã¨å¹¸ã›ã«ãªã‚Œã‚‹ã‹ã‚‚
" function! s:MyFunc()
"   " EXã‚³ãƒãƒ³ãƒ‰(ex-cmd-indexã‚‚ã—ãã¯expression-commands)ã¯ãã®ã¾ã¾å‘¼ã³å‡ºã›ã‚‹
"     echo "foo"
"     normal gg
"   " å¼ã‚’è©•ä¾¡ã—ã¦ãã‚Œãªã„ã‚‚ã®(å¤‰æ•°ã‚’ãã®ã¾ã¾æ–‡å­—åˆ—ã¨ã—ã¦è§£é‡ˆã—ã¦ã—ã¾ã†ã‚‚ã®)ã«ã¤ã„ã¦ã¯executeã‚’ä½¿ã†
"     let l:tabNumber = 1
"     " é–“é•ã„
"     tabnext l:tabNumber
"     " æ­£è§£
"     execute "tabnext " . l:tabNumber
"   " çµ„ã¿è¾¼ã¿é–¢æ•°(functionsã‚‚ã—ãã¯function-list)ã‚„è‡ªä½œé–¢æ•°ã‚’'ç›´æ¥'å‘¼ã³å‡ºã™ã¨ãã«ã¯callã‚’ä½¿ã†
"   " ãŸã ã—letã‚„echoãªã©å¼ã‚’è©•ä¾¡ã™ã‚‹ã‚‚ã®ã®å¾Œã‚ã§ã‚ã‚Œã°callã¯å¿…è¦ãªã„
"     call feedkeys("gg")
"   " keypressã‚’emulateã™ã‚‹ã«ã¯normal(EXã‚³ãƒãƒ³ãƒ‰)ã‚‚ã—ãã¯feedkeys(é–¢æ•°)ã‚’ä½¿ã†
"   " å¤–éƒ¨ã‚³ãƒãƒ³ãƒ‰(ã‚·ã‚§ãƒ«ã‚³ãƒãƒ³ãƒ‰)ã¯system
"   " ç‰¹æ®Šãªè¡¨ç¾ã‚’æ–‡å­—åˆ—ã«å±•é–‹ã—ãŸã„ã¨ãã¯expand
" endfunction

command! FooBarTest call s:FooBarTest()
function! s:FooBarTest(...)
  echo "bar"
  return ""
endfunction

" ç¾åœ¨ãƒ•ã‚¡ã‚¤ãƒ«ã®ä½ç½®ã«ç§»å‹•ã™ã‚‹ã‚³ãƒãƒ³ãƒ‰
" ã‚³ãƒãƒ³ãƒ‰ã¯å°†æ¥çš„ã«åˆ¥ãƒ•ã‚¡ã‚¤ãƒ«ã«ã—ãŸã»ã†ãŒã„ã„ã‹ã‚‚ã€‚
" kaoriyaã®å ´åˆã€cmdex.vimã«ã¾ã£ãŸãåŒã˜ã‚‚ã®ãŒCdCurrentã§å®šç¾©ã—ã¦ã‚ã‚‹ã€‚
" command! -nargs=0 Cd cd %:p:h
command! Cd call s:CdToGitRoot(v:false)
command! Lcd call s:CdToGitRoot(v:true)

function! s:GetGitRootPath(...)
  if a:0 > 1
    echoerr "invalid args"
    return ""
  endif

  " current file for no arg
  " let l:targetPath = a:0 == 0 ? escape(expand('%:p:h'), ' ') : a:1
  let l:targetPath = a:0 == 0 ? expand('%:p:h') : a:1
  if l:targetPath[1] == ':' && l:targetPath[2] == '\' && l:targetPath[3] == ''
    " Do nothing, workaround?
  else
    let l:targetPath = l:targetPath . ';'
  endif
  let l:result = finddir('.git', l:targetPath)
  if !empty(l:result)
    " let l:result = fnameescape(fnamemodify(l:result, ':p:h:h'))
    let l:result = fnamemodify(l:result, ':p:h:h')
  endif
  return l:result
endfunction

function! s:CdToGitRoot(isLcd)
  let l:gitRootPath = s:GetGitRootPath()
  if empty(l:gitRootPath)
    echo "no root..."
  else
    if a:isLcd
      execute "lcd " . l:gitRootPath
    else
      execute "cd " . l:gitRootPath
    endif
    echo l:gitRootPath
  endif
endfunction

function! s:DoGrep()
  let l:warnings = ""
  let l:gitRootOfPwd = s:GetGitRootPath(getcwd())
  if l:gitRootOfPwd != s:GetGitRootPath()
    let l:warnings = l:warnings . "NOT the same repository, "
  endif
  if empty(l:gitRootOfPwd)
    let l:warnings = l:warnings . "NOT a git repository, "
  endif
  if l:warnings != ""
    echohl ErrorMsg | echo "Caution: " . l:warnings . "continue... " | echohl None
    let l:c = getchar()
    " ã‚„ã‚ŠãŸã„ã®ã¯ == "\<esc>" ãªã‚“ã ã‘ã©ã€ã†ã¾ãã„ã‹ãªã„ã€‚ç›´ã—ãŸã„ã€‚ã€‚
    if l:c == 27 | redraw | echo "" | return | endif " ã‚‚ã£ã¨ã†ã¾ãæ¶ˆã™æ–¹æ³•ã¯ãªã„ã‚‚ã®ã‹ã€‚ã€‚
  endif
  if has('kaoriya')
    let l:keyHeadStr = ":tabnew \<bar> set transparency=200 \<bar> grep -iE"
  else
    let l:keyHeadStr = ":tabnew \<bar> grep -iE"
  endif
  if empty(l:gitRootOfPwd)
    let l:keyHeadStr = l:keyHeadStr . " --no-index"
  endif
  call feedkeys(l:keyHeadStr . " \"\" \<bar> cw\<left>\<left>\<left>\<left>\<left>\<left>")
endfunction

command! CloseRightTabs call s:CloseRightTabs()

function! s:CloseRightTabs()
  " è‡ªåˆ†è‡ªèº«ã¯é–‰ã˜ãªã„ã®ã§"+1"
  let l:firstTabNumberToClose = tabpagenr() + 1
  let l:totalTabCount = tabpagenr('$')
  " å¾Œã‚ã‹ã‚‰é †ã«é–‰ã˜ã¦ã„ã
  " å‰ã‹ã‚‰ã ã¨tab numberãŒå¸¸ã«æ›´æ–°ã•ã‚Œã‚‹ãŸã‚åŒã˜tab numberã‚’é–‰ã˜ç¶šã‘ã‚‹å¿…è¦ãŒã‚ã‚Šã€
  " ãã†ã™ã‚‹ã¨ã‚¨ãƒ©ãƒ¼ãŒèµ·ããŸã¨ãã ã‘é–‰ã˜ã‚‹tab numberã‚’incrementã—ãªã‘ã‚Œã°ãªã‚‰ãšå‡¦ç†ãŒé¢å€’
  " æœªä¿å­˜ã®å¤‰æ›´ãªã©ã§é–‰ã˜ã‚‹ã“ã¨ãŒã§ããªã‹ã£ãŸå ´åˆã€ãã®tabãŸã¡ã ã‘ãŒæ®‹ã‚‹
  for currTabNumber in range(l:firstTabNumberToClose, l:totalTabCount)
    " ã‚„ã‚„ã“ã—ã„ã€‚ã€‚ã€‚
    let l:tabNumberToClose = l:totalTabCount - (currTabNumber - l:firstTabNumberToClose)
    execute "tabclose " . l:tabNumberToClose
  endfor
  echo "done!"
endfunction

command! Ccl call s:Ccl()

function! s:Ccl()
  let l:orgTabNumber = tabpagenr()
  for currTabNumber in range(1, tabpagenr('$'))
    execute "tabnext " . currTabNumber
    ccl
  endfor
  execute "tabnext " . l:orgTabNumber
endfunction

function! s:GoToFirstColumn()
  let l:orgColumn = col(".")
  " ä¸€åº¦'^'ã§ç§»å‹•
  normal ^
  if l:orgColumn != 1 && l:orgColumn <= col(".")
    " 1åˆ—ç›®ã ã£ãŸã‚‰ä½•ã‚‚ã—ãªã„('^'ã§ã‚„ã‚ã‚‹)
    " å…ˆé ­ã‚ˆã‚Šå‰ã ã£ãŸã‚‰ã•ã‚‰ã«'0'ã«ç§»å‹•
    normal! 0
  endif
endfunction

command! CGrep call s:CGrep()

function! s:CGrep()
  let l:found = v:false
  while v:true
    let l:currList = getqflist({'all': 1})
    if stridx(l:currList["title"], "grep") > 0 | let l:found = v:true | endif
    if l:found || l:currList["nr"] <= 1
      break
    else
      silent colder
    endif
  endwhile
  if l:found | echo "found" | else | echo "not found" | endif
endfunction

command! -nargs=1 CGoTo call s:CGoTo(<f-args>)

function! s:GetCurrQuickFixListNumber()
  return getqflist({"nr": 0})["nr"]
endfunction

function! s:CGoTo(listNumber)
  if a:listNumber < 1 || a:listNumber > 10 | echo "invalid" | return | endif
  let l:currListNumber = s:GetCurrQuickFixListNumber()
  let l:diff = abs(l:currListNumber - a:listNumber)
  if (l:currListNumber > a:listNumber)
    execute("silent" . l:diff . "colder")
  elseif (l:currListNumber < a:listNumber)
    execute("silent" . l:diff . "cnewer")
  endif
endfunction

function! s:BsForInsertMode()
  let l:text = getline('.')
  if l:text =~# '^\s\+$'
    return "\<c-u>" " ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã®ã¿ã®å ´åˆã¯ã™ã¹ã¦æ¶ˆã™
  elseif exists('b:comment_text') && l:text =~# '^\s*' . b:comment_text . ' *$'
    return repeat("\<c-h>", strlen(l:text) - stridx(l:text, b:comment_text))
  else
    return "\<c-h>" " ãã‚Œä»¥å¤–ã®å ´åˆã¯1æ–‡å­—æ¶ˆã™
  endif
endfunction

if &statusline !=# '%!MyStatusLine()'
  set statusline=%f%m%r%h%w%=%v\ [%{&fileformat},\ %{&fileencoding}]
endif

function! g:MyStatusLine()
  if empty(&fileencoding)
    return "%f%m%r%h%w\ /\ %{cfi#format('%s','')}%=%v\ [%{&fileformat},\ %{&encoding}]"
  else
    return "%f%m%r%h%w\ /\ %{cfi#format('%s','')}%=%v\ [%{&fileformat},\ %{&fileencoding}]"
  endif
endfunction

function! s:ToggleComment() range
  if !exists('b:comment_text') | echoerr 'no b:comment_text' | return | endif
  let l:comment_text = escape(b:comment_text, '/$.*~')
  if getline(a:firstline) =~ '^\s*' . l:comment_text . ' '
    " uncomment with a space
    let l:substitute_text = a:firstline . ',' . a:lastline . 's/\m^\(\s*\)' . l:comment_text . ' \(.*\)/\1\2/g'
  elseif getline(a:firstline) =~ '^\s*' . l:comment_text
    " uncomment with no space
    let l:substitute_text = a:firstline . ',' . a:lastline . 's/\m^\(\s*\)' . l:comment_text . '\(.*\)/\1\2/g'
  else
    " comment out
    let l:substitute_text = a:firstline . ',' . a:lastline . 's/\m\(.*\)/' . l:comment_text . ' \1/g'
  endif
  execute l:substitute_text
endfunction

function! s:ChangeFontSize(diff)
  let l:sizeStartPos = stridx(&guifont, ":h")
  if (l:sizeStartPos == -1)
    echoerr "err"
    return
  else
    let l:sizeStartPos += 2 " æ•°å­—é–‹å§‹éƒ¨åˆ†ã¾ã§ç§»å‹•
  endif
  let l:sizeEndPos = stridx(&guifont, ":", l:sizeStartPos)
  if (l:sizeEndPos == -1)
    echoerr "err"
    return
  else
    let l:sizeEndPos -= 1 " æ•°å­—çµ‚äº†éƒ¨åˆ†ã¾ã§ç§»å‹•
  endif
  let l:orgFontSize = str2nr(&guifont[l:sizeStartPos:l:sizeEndPos])
  let l:newFontSize = printf("%d", l:orgFontSize + a:diff)
  let &guifont = &guifont[0:(l:sizeStartPos-1)] . l:newFontSize . &guifont[(l:sizeEndPos+1):-1]
endfunction

command! ReloadWithEucJp e ++enc=euc-jp
command! Term vert term ++noclose bash
command! TermDot vert new | lcd ~/dotfiles | term ++noclose ++curwin bash

function! s:Redir(command)
  if has('clipboard')
    " clipboard+=unnamed ã‚’ä½¿ã†å ´åˆã¯ã€" ã§ã¯ãªã * ã‹ã‚‰ãƒšãƒ¼ã‚¹ãƒˆã•ã‚Œã‚‹ã®ã§ã“ã£ã¡ã®ã»ã†ãŒéƒ½åˆãŒã„ã„
    redir @*
  else
    redir @"
  endif
  silent execute a:command
  redir END
endfunction

function! s:MoveUpwardDownward(upward)
  let l:searchStr = '^' . getline('.')[:getcurpos()[2]-2] . '\S'
  if a:upward
    call search(l:searchStr, 'bez')
  else
    call search(l:searchStr, 'ez')
  endif
endfunction

function! s:PasteSlash() abort
  if @/[0:1] ==? '\v'
    return @/[2:-1]
  else
    return @/
  endif
endfunction

command! -nargs=1 -complete=command Redir call s:Redir(<f-args>)

set secure

" modeline
" vim: expandtab tabstop=2 textwidth=0
